# U-Boot: Marvell Kirkwood platforms
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=2

pkgname=uboot-pogo_e02
pkgver=2014.04
pkgrel=1
arch=('arm')
pkgdesc="U-Boot for the Pogoplug V2 (E02)"
url="https://github.com/archlinuxarm/u-boot"
depends=('uboot-env' 'mtd-utils')
makedepends=('git')
license=('GPL')
install='uboot-pogo_e02.install'
source=("git+https://github.com/archlinuxarm/u-boot.git#branch=2014.04-E02-WIP"
         http://archlinuxarm.org/os/armv5te/boot/pogo_e02/uboot-pogo_e02.env)
md5sums=('SKIP'
         'e707982899cebacd64878a87208fa750')

build() {
  cd u-boot
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  make distclean
  make pogo_e02_config
  make u-boot.kwb
  dd if=u-boot.kwb of=uboot-pogo_e02.kwb bs=512k conv=sync
}

package() {
  mkdir -p "${pkgdir}"/boot
  cp u-boot/uboot-pogo_e02.kwb "${pkgdir}"/boot
  cp "${srcdir}"/uboot-pogo_e02.env "${pkgdir}"/boot
}
