# U-Boot: Marvell Kirkwood platforms
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=2

pkgname=uboot-dockstar
pkgver=2014.04
pkgrel=1
arch=('arm')
pkgdesc="U-Boot for the Seagate FreeAgent DockStar"
url="https://github.com/archlinuxarm/u-boot"
depends=('uboot-env' 'mtd-utils')
makedepends=('git')
license=('GPL')
install='uboot-dockstar.install'
source=("git+https://github.com/archlinuxarm/u-boot.git#branch=2014.04-DockStar-WIP"
         http://archlinuxarm.org/os/armv5te/boot/dockstar/uboot-dockstar.env)
md5sums=('SKIP'
         '483bb63ff84c05d96e43ec4c85b8d7c2')

build() {
  cd u-boot
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  make distclean
  make dockstar_config
  make u-boot.kwb
  dd if=u-boot.kwb of=uboot-dockstar.kwb bs=512k conv=sync
}

package() {
  mkdir -p "${pkgdir}"/boot
  cp u-boot/uboot-dockstar.kwb "${pkgdir}"/boot
  cp "${srcdir}"/uboot-dockstar.env "${pkgdir}"/boot
}
